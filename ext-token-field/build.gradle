buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
		vaadinVersion = '7.5.7'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

System.setProperty("file.encoding", "UTF-8")

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'spring-boot' 
apply plugin: 'war'

group = 'com.explicatis'
version = '0.1.4'

war {
    baseName = 'ExtTokenField'
}

defaultTasks 'jar'

ext.mavenUrl = 'http://maven.vaadin.com/vaadin-addons'

mainClassName = ''

repositories {
    mavenCentral()
}

configurations {
	providedRuntime
    gwt
    compile.exclude module: "hibernate-entitymanager"
}

dependencies {
    compile group: 'com.vaadin', name: 'vaadin-client', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-themes', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-server', version: vaadinVersion
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    gwt group: 'com.vaadin', name: 'vaadin-client-compiler', version: vaadinVersion
    
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}")
}

ext.widgetSetName = 'com.explicatis.ext_token_field.WidgetSet'
ext.widgetSetBuildDir = 'src/main/webapp/VAADIN/widgetsets'

ext.compileClassPath = files(
        sourceSets.main.java.srcDirs,
        sourceSets.main.output.classesDir,
        configurations.compile.files,
        configurations.gwt.files
)

compileJava {
	options.encoding = 'UTF-8'
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    String java6Home = System.getenv()['JAVA_1_6_HOME']
    if (java6Home) {
        if (System.getProperty("os.name").toLowerCase().contains('mac')) {
            options.bootClasspath = "${java6Home}/../Classes/classes.jar"
        } else {
            options.bootClasspath = "${java6Home}/jre/lib/rt.jar"
        }
    }
}

clean {
    dependsOn 'cleanCompileWidgetSet'
}

task compileWidgetSet {
    dependsOn 'classes'

    inputs.files compileClassPath
    inputs.files sourceSets.main.java.srcDirs
    outputs.dir widgetSetBuildDir

    doLast {
        javaexec {
            maxHeapSize = '512m'
            jvmArgs = ['-Xss1024k', '-Djava.awt.headless=true']
            classpath = compileClassPath
            main = 'com.google.gwt.dev.Compiler'
            args = ['-war', widgetSetBuildDir, widgetSetName]
        }

        delete file("${widgetSetBuildDir}/WEB-INF")
    }
}


task devmode(type: JavaExec) {
    classpath = compileClassPath
    main = 'com.google.gwt.dev.DevMode'
    jvmArgs = ['-XX:MaxPermSize=400m', '-Xmx800m']
    args = ['-noserver', '-war', widgetSetBuildDir, widgetSetName, '-startupUrl', 'http://localhost:8080/test', '-bindAddress', '0.0.0.0']
}

task superdevmode(type: JavaExec) {
    classpath = compileClassPath
    main = 'com.google.gwt.dev.codeserver.CodeServer'
    jvmArgs = ['-XX:MaxPermSize=256m', '-Xmx512m']
    args = [widgetSetName]
}

jar {
    baseName 'ext-token-field'
    from (sourceSets.main.output.classesDir) {
    	exclude 'rebel.xml'
    }
    from (sourceSets.main.java.srcDirs) {
    	include '**/WidgetSet.gwt.xml'
    	include '**/public/**'
    	include '**/*.java'
    }
    manifest {
        attributes(
        	'Vaadin-Package-Version': '1',
            'Vaadin-Widgetsets': widgetSetName,
            'Implementation-Title': 'Extended Token Field',
            'Implementation-Version': version,
			'Vaadin-License-Title': 'Apache License 2.0',
            'Vaadin-License-File': 'http://www.apache.org/licenses/LICENSE-2.0.html'
        )
    }
}

task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    include '**/*.java'
    exclude 'rebel.xml'
}

artifacts {
    archives jar
    archives sourceJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url mavenUrl
        }
    }
}

task fakeJar(type: Jar) {
	manifest {
    attributes(
        'Implementation-Title': 'Extended Token Field',
        'Implementation-Version': version,
        'Vaadin-Package-Version': '1',
        'Vaadin-License-Title': 'Apache License 2.0',
        'Vaadin-License-File': 'http://www.apache.org/licenses/LICENSE-2.0.html',
        'Vaadin-Addon': "ext-token-field-${project.version}.jar"
    )
    }
    fakeJar.manifest.writeTo("$buildDir/tmp/fakeJar/MANIFEST.MF")
}

task buildZip(type: Zip) {
    dependsOn fakeJar
	from jar
	from sourceJar
	into ('META-INF') {
	    from ("$buildDir/tmp/fakeJar")
		include 'MANIFEST.MF'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}
