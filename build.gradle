buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
		vaadinVersion = '7.4.7'
		vaadinSpringVersion = '1.0.0.beta3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'spring-boot' 

group = 'com.explicatis'
version = '0.0.9'

defaultTasks 'jar'

ext.nexusUrl='http://nexus:8081/nexus/content/repositories/repository'
ext.nexusUser='admin'
ext.nexusPwd='admin123'

mainClassName = ''

repositories {
    mavenCentral()
}

configurations {
    gwt
    compile.exclude module: "hibernate-entitymanager"
}

dependencies {
    compile group: 'com.vaadin', name: 'vaadin-client', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-themes', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-server', version: vaadinVersion
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile "com.vaadin:vaadin-spring-boot:${vaadinSpringVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    gwt group: 'com.vaadin', name: 'vaadin-client-compiler', version: vaadinVersion
}

ext.widgetSetName = 'com.explicatis.ext_token_field.WidgetSet'
ext.widgetSetBuildDir = 'src/main/webapp/VAADIN/widgetsets'

ext.compileClassPath = files(
        sourceSets.main.java.srcDirs,
        sourceSets.main.output.classesDir,
        configurations.compile.files,
        configurations.gwt.files
)

compileJava {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    String java6Home = System.getenv()['JAVA_1_6_HOME']
    if (java6Home) {
        if (System.getProperty("os.name").toLowerCase().contains('mac')) {
            options.bootClasspath = "${java6Home}/../Classes/classes.jar"
        } else {
            options.bootClasspath = "${java6Home}/jre/lib/rt.jar"
        }
    }
}

clean {
    dependsOn 'cleanCompileWidgetSet'
}

task compileWidgetSet {
    dependsOn 'classes'

    inputs.files compileClassPath
    inputs.files sourceSets.main.java.srcDirs
    outputs.dir widgetSetBuildDir

    doLast {
        javaexec {
            maxHeapSize = '512m'
            jvmArgs = ['-Xss1024k', '-Djava.awt.headless=true']
            classpath = compileClassPath
            main = 'com.google.gwt.dev.Compiler'
            args = ['-war', widgetSetBuildDir, widgetSetName]
        }

        delete file("${widgetSetBuildDir}/WEB-INF")
    }
}


task devmode(type: JavaExec) {
    classpath = compileClassPath
    main = 'com.google.gwt.dev.DevMode'
    jvmArgs = ['-XX:MaxPermSize=400m', '-Xmx800m']
    args = ['-noserver', '-war', widgetSetBuildDir, widgetSetName, '-startupUrl', 'http://localhost:8080/test', '-bindAddress', '0.0.0.0']
}

task superdevmode(type: JavaExec) {
    classpath = compileClassPath
    main = 'com.google.gwt.dev.codeserver.CodeServer'
    jvmArgs = ['-XX:MaxPermSize=256m', '-Xmx512m']
    args = [widgetSetName]
}

jar {
    baseName 'ext-token-field'
    from sourceSets.main.output.classesDir
    exclude 'rebel.xml'
    manifest {
        attributes(
            'Vaadin-Widgetsets': widgetSetName,
            'Implementation-Vendor': 'Explicatis GmbH (ext-token-field@explicatis.com), Florian Schmitt',
            'Implementation-Title': 'Extended Tokenfield',
            'Implementation-Version': version ,
            'Vaadin-Package-Version': '1',
            'Vaadin-License-Title': 'Apache License 2.0',
            'Vaadin-License-File': 'http://www.apache.org/licenses/LICENSE-2.0.html'
        )
    }
}
/*
task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}*/

task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    include '**/*.java'
    exclude 'rebel.xml'
    manifest {
        attributes(
            'Vaadin-Widgetsets': widgetSetName,
            'Implementation-Vendor': 'Explicatis GmbH (ext-token-field@explicatis.com), Florian Schmitt',
            'Implementation-Title': 'Extended Tokenfield',
            'Implementation-Version': version ,
            'Vaadin-Package-Version': '1',
            'Vaadin-License-Title': 'Apache License 2.0',
            'Vaadin-License-File': 'http://www.apache.org/licenses/LICENSE-2.0.html'
        )
    }
}

artifacts {
    archives jar
    archives sourceJar
   /* archives javadocJar*/
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact sourceJar
            /*artifact javadocJar*/
        }
    }
    repositories {
        maven {
            url nexusUrl
            credentials {
                username = nexusUser
                password = nexusPwd
            }
        }
    }
}